C
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C        OUT2D FORTRAN
      SUBROUTINE OUT2D(T,CHAR,IUNIT,IB,IE,ISKP,JB,JE,JSKP,ID,JD)
C
C      SUBROUTINE TO OUTPUT A TWO-DIMENSIONAL ARRAY SO THAT IT CAN
C      BE EASILY SCANNED.
C
C      REAL T(ID,JD)= THE ARRAY TO BE OUTPUT
C      CHARACTER*8 CHAR= AN ALPHANUMERIC WORD DESCRIBING THE ARRAY
C      INTEGER IUNIT= THE UNIT NUMBER TO WHICH THE ARRAY IS TO BE SENT
C      INTEGER IB,IE= BEGINNING AND END INDEX VALUES IN THE I-DIRECTION
C      INTEGER ISKP= THE COLUMN SKIPPING IN THE I-DIRECTION
C            IE. DO 10 I=IB,IE,ISKP
C      INTEGER JB,JE= BEGINNING AND END INDEX VALUES IN THE J-DIRECTION
C      INTEGER JSKP= THE SPACING IN THE J-DIRECTION
C      INTEGER ID,JD= DIMENSION OF THE ARRAY T
C
C      NOTES:
C            ELEVEN COLUMNS ARE PRINTED ACROSS THE PAGE IN EACH
C            SECTION FOR AS MANY SECTIONS AS ARE REQUIRED.
C
C            CHAR SHOULD CONTAIN 8 CHARACTERS INCUDING BLANKS
C      
C      IMPLEMENTED BY J. VAN DOORMAAL APRIL 1, 1982
C
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      REAL T(ID,JD)
      INTEGER SCOUNT
      CHARACTER*8 CHAR
      INTEGER PNTI(1000),PNTJ(1000)
      INTEGER IUNIT,IB,IE,ISKP,JB,JE,JSKP,ID,JD
      INTEGER LI,LJ,NWORD,MF,MB,I,J,J1,J2,K,IESCNT
*     
      PARAMETER (NWORD=06)
*      
      LI=0
      LJ=0
      DO 10 I=IB,IE,ISKP
      LI=LI+1
   10 PNTI(LI)=I
      DO 20 J=JB,JE,JSKP
      LJ=LJ+1
   20 PNTJ(LJ)=J
      WRITE(IUNIT,104) CHAR
      SCOUNT=0
   30 SCOUNT=SCOUNT+1
      IESCNT=SCOUNT*NWORD
      MF=MIN0(LI,IESCNT)
      MB=IESCNT-NWORD+1
      DO 40 J1=1,LJ
      J2=LJ-J1+1
   40 WRITE(IUNIT,100)PNTJ(J2),(T(PNTI(K),PNTJ(J2)),K=MB,MF)
      WRITE(IUNIT,102) (PNTI(K),K=MB,MF)
      IF(LI.GT.MF) GOTO 30
  100 FORMAT(' ',I3,' ',11(1PE11.3))
  102 FORMAT('  J/I',I4,10(7X,I4))
  104 FORMAT(' ',/,' ',A8)
      RETURN
      END

